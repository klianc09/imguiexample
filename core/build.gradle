[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
eclipse.project.name = appName + '-core'

import org.gradle.internal.os.OperatingSystem
switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"

    compile "com.github.kotlin-graphics:imgui:$imguiVersion"

    // Look up which modules and versions of LWJGL are required and add the appropriate natives.
    configurations.compile.resolvedConfiguration.getResolvedArtifacts().forEach {
        if (it.moduleVersion.id.group == "org.lwjgl") {
            if(it.moduleVersion.id.name == "lwjgl-ovr" && (lwjglNatives != "natives-windows"))
                return //lwjgl-ovr natives are only available for windows
            if(it.moduleVersion.id.name == "lwjgl-vulkan" && (lwjglNatives != "natives-macos"))
                return //lwjgl-vulkan natives are only available for macos

            runtime "org.lwjgl:${it.moduleVersion.id.name}:${it.moduleVersion.id.version}:${lwjglNatives}"
        }
    }

}